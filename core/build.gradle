plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'jacoco'
    id 'com.jfrog.bintray' version '1.8.5'
}

sourceSets {
    main {
        resources {
            exclude '**/node_modules/*'
        }
    }
}

task myJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = project.sourceSets.main.compileClasspath
    options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
}

dependencies {
    api platform(project(':platform'))

    api 'org.slf4j:slf4j-api'
    api 'org.ow2.asm:asm'

    compileOnly 'com.fasterxml.jackson.core:jackson-databind'
    compileOnly 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compileOnly 'com.google.code.gson:gson'
    compileOnly 'org.eclipse:yasson'

    compileOnly 'org.xerial:sqlite-jdbc'
    compileOnly 'com.h2database:h2'
    compileOnly 'org.postgresql:postgresql'
    compileOnly 'com.google.cloud.sql:postgres-socket-factory'
    compileOnly 'com.oracle.ojdbc:ojdbc8_g'
    compileOnly 'org.mariadb.jdbc:mariadb-java-client'
    compileOnly 'mysql:mysql-connector-java'
    compileOnly 'com.microsoft.sqlserver:mssql-jdbc'
    compileOnly 'com.ibm.db2.jcc:db2jcc'
    compileOnly 'com.zaxxer:HikariCP'
    compileOnly 'org.apache.commons:commons-dbcp2'
    compileOnly 'com.mchange:c3p0'
    compileOnly 'org.apache.tomcat:tomcat-jdbc'
    compileOnly 'redis.clients:jedis'
    compileOnly 'io.lettuce:lettuce-core'
    compileOnly 'org.mongodb:mongo-java-driver'
    compileOnly 'org.elasticsearch.client:elasticsearch-rest-high-level-client'

    testImplementation 'org.ow2.asm:asm-util'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    testImplementation 'com.google.code.gson:gson'
    testImplementation 'org.eclipse:yasson'
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mariadb'
    testImplementation 'org.testcontainers:mssqlserver'
    testImplementation 'org.testcontainers:mysql'
    testImplementation 'org.testcontainers:oracle-xe'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:db2'
    testImplementation 'org.testcontainers:elasticsearch'
    testImplementation 'org.xerial:sqlite-jdbc'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.postgresql:postgresql'
    testImplementation 'com.google.cloud.sql:postgres-socket-factory'
    testImplementation 'com.microsoft.sqlserver:mssql-jdbc'
    testImplementation 'com.ibm.db2.jcc:db2jcc'
    testImplementation 'com.oracle.ojdbc:ojdbc8_g'
    testImplementation 'org.mariadb.jdbc:mariadb-java-client'
    testImplementation 'mysql:mysql-connector-java'
    testImplementation 'com.zaxxer:HikariCP'
    testImplementation 'org.apache.commons:commons-dbcp2'
    testImplementation 'com.mchange:c3p0'
    testImplementation 'org.apache.tomcat:tomcat-jdbc'
    testImplementation 'redis.clients:jedis'
    testImplementation 'io.lettuce:lettuce-core'
    testImplementation 'org.mongodb:mongo-java-driver'
    testImplementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client'

    testFixturesApi 'org.assertj:assertj-core'
    testFixturesApi 'org.junit.jupiter:junit-jupiter'
    testFixturesApi 'net.javacrumbs.json-unit:json-unit-assertj'
    testFixturesApi 'ch.qos.logback:logback-classic'
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks['jacocoTestReport'].dependsOn test

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

jar {
    manifest {
        attributes(
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Bundle-ContactAddress': 'https://github.com/jobrunr/jobrunr',
                'Bundle-Description': 'JobRunr library',
                'Bundle-ManifestVersion': 2,
                'Bundle-Name': 'JobRunr',
                'Bundle-SymbolicName': 'org.jobrunr.jobrunr',
                'Bundle-Vendor': 'JobRunr',
                'Bundle-Version': project.version.replace("v", ""),
                'Implementation-Build-Date':  new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Implementation-Title': 'JobRunr',
                'Implementation-Vendor': 'JobRunr',
                'Implementation-Vendor-Id': 'org.jobrunr.jobrunr',
                'Implementation-Version': project.version.replace("v", ""),
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.jobrunr'
            artifactId = 'jobrunr'
            version = project.version.replace("v", "")
            from components.java

            pom {
                name = 'JobRunr'
                description = 'An easy way to perform background processing on the JVM. Backed by persistent storage. Open and free for commercial use.'
                url = 'https://github.com/jobrunr/jobrunr'
                licenses {
                    license {
                        name = 'Multi licensed'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md'
                    }
                }
                developers {
                    developer {
                        id = 'rdehuyss'
                        name = 'Ronald Dehuysser'
                        email = 'ronald.dehuysser@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jobrunr/jobrunr.git'
                    developerConnection = 'scm:git@github.com:jobrunr/jobrunr.git'
                    url = 'https://github.com/jobrunr/jobrunr.git'
                }

                versionMapping {
                    usage('java-runtime'){
                        fromResolutionResult()
                    }
                }

                withXml {
                    Node pomNode = asNode()
                    pomNode.remove(pomNode.dependencyManagement)
                    pomNode.dependencies.'*'.findAll() {
                        it.optional.text() == 'true'
                    }.each() {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_APIKEY')
    publications = ['mavenJava']
    publish = true

    pkg {
        userOrg = 'jobrunr'
        repo = 'jobrunr'
        name = 'JobRunr'
        licenses = ['LGPL-3.0']
        websiteUrl = 'https://www.jobrunr.io'
        vcsUrl = 'https://github.com/jobrunr/jobrunr.git'
        issueTrackerUrl = 'https://github.com/jobrunr/jobrunr/issues'
        version {
            name = project.version.replace("v", "")
            released  = new Date()
            vcsTag = project.version
        }
    }
}